      SUBROUTINE OBSIO(TAPIN,TAPOUT,TYPE,IO,IORTRN)
      IMPLICIT NONE

      include 'pepglob.inc'

      INTEGER*4 TAPIN,TAPOUT,TYPE,IO,IORTRN
C
C        SUBR. IOPEP - R. GOLDSTEIN - DEC. 1976
C             - BASED ON SUBR. RDLIB OF ABC
C        REVISED AND RENAMED OBSIO - J.F.CHANDLER - 1980 APRIL
C        LINK LEVEL AND DT CHANGES - JFC - 1980 SEP
C     SUBROUTINE TO READ AND WRITE OBSLIB TAPES
C     OBSLIB TAPES HAVE FOUR DATA TYPES ON THEM
C     TYPES ONE AND TWO ARE TAPE INITIALIZATION AND OCCUR
C     ONLY AT THE START OF A TAPE
C     TYPE THREE INITIATE DATA SERIES
C     TYPE FOUR CONTAIN THE DATA FOR EACH OBSERVATION
C     WITHIN THE SERIES
C     TYPE FOUR RECORDS WITHIN A SERIES OUGHT TO BE TIME SEQUENTIAL
C     BUT THE DATA SERIES THEMSELVES HAVE NO TIME CONSTRAINTS
C
C
C        TAPIN     INPUT TAPE
C        TAPOUT    OUTPUT TAPE
C        TYPE      RECORD TYPE. CAN BE 1,2,3,4
C                  IF 0, THEN REWIND BOTH TAPES
C        IO        READ,WRITE CONTROL
C             +2     WRITE NULL RECORD (TYPE 4 ONLY)
C             +1     WRITE ONLY
C              0     READ AND WRITE
C             -1     READ ONLY (BUT WRITE A NULL TYPE 4 RECORD IF EOF
C                    ENCOUNTERED WITHIN A SERIES, PROVIDED TAPOUT IS
C                    POSITIVE)
C             -2     ABSOLUTELY READ ONLY
C
C        IORTRN    RETURN CODE
C             -3     ILLEGAL TYPE SPECIFIED
C             -2     I/O ERRORS
C             -1     END OF FILE ENCOUNTERED
C              0     END OF TYPE 3 OR 4 RECORDS
C              1     SUCESSFUL COMPLETION
C
C
      include 't1a.inc'
      include 't2a.inc'
      include 't3a.inc'
      include 't4a.inc'
C
C           LOCAL
      INTEGER*2 NSKY1
      INTEGER*4 IZERO/0/
      CHARACTER*4 OBSIOL/'CPY '/
      INTEGER*4 I,J,NEXT1
C
      IORTRN=1
      IF(TYPE.LE.0) GOTO 50
      GOTO (100,200,300,400),TYPE
C        ILLEGAL TYPE
      IORTRN=-3
      GOTO 990
C
C        REWIND TAPES
   50 IF(TAPIN.GT.0) REWIND TAPIN
      IF(TAPOUT.LE.0) GOTO 990
      ENDFILE TAPOUT
      REWIND TAPOUT
      GOTO 990
C
C
C        TYPE 1
  100 IF(IO.GT.0.OR.TAPIN.LE.0) GOTO 105
      READ(TAPIN,ERR=910,END=920) R1A
      LNKLVA=OBSIOL
C
  105 IF(IO.LT.0.OR.TAPOUT.LE.0) GOTO 990
      WRITE(TAPOUT) R1A,LNKLVA
      GOTO 990
C
C        TYPE 2
C
  200 IF(IO.GT.0.OR.TAPIN.LE.0) GOTO 205
C           CLEAR LDTA VECTOR BEFORE READING
      DO 202 I=1,600
  202 LDTA(I)=0
      READ(TAPIN,ERR=910,END=920)
     1 NTAPA,NPG2A,ITRTA,NPRMXA,NCNMXA,JDGS0A,(PRMA(I),I=1,NPRMXA),
     2 ((GSCNA(I,J),I=1,NCNMXA),J=1,4),(LPRMA(I),I=1,NPRMXA),
     3 ((LGSA(I,J),I=1,NCNMXA),J=1,4),
     4 NUMDTA,NMDT1A,(JDDTA(I),I=1,NMDT1A),(DTA(I),I=1,NMDT1A),
     5 (LDTA(I),I=1,NMDT1A),NLABA,(LABA(I),I=1,NLABA),JDDT0A,LNKLVA,
     6 NSITCA,NSPTCA
      IF(NPRMXA.GT.NMPRM .OR. NCNMXA.GT.NMBOD) CALL SUICID(
     . 'TOO MANY PARAMETERS ON TYPE 2 RECORD, STOP IN OBSIO ',13)
      DO I=NPRMXA+1,NMPRM
         PRMA(I)=0._10
         LPRMA(I)=0
      END DO
      DO I=NCNMXA+1,NMBOD
         DO J=1,4
            GSCNA(I,J)=0._10
            LGSA(I,J)=0
         END DO
      END DO
      IF(NUMDTA.LE.0) LDTA(1)=0
      IF(NUMDTA.GT.0) CALL DTCHCK(JDDT0A,NUMDTA,NMDT1A,DTA,LDTA)
      IF(NLABA.LE.0) NLABA=1
      IF(NSITCA.NE.6) NSITCA=3
      NSITCO=NSITCA
      IF(NSPTCA.NE.6) NSPTCA=3
      NSPTCO=NSPTCA
C
  205 IF(IO.LT.0.OR.TAPOUT.LE.0) GOTO 990
      IF(NMDT1A.LE.0) NMDT1A=1
      IF(NLABA.LE.0) NLABA=1
      IF(NSITCA.NE.6) NSITCA=3
      IF(NSITCO.NE.3 .AND. NSITCO.NE.6) NSITCO=NSITCA
      IF(NSPTCA.NE.6) NSPTCA=3
      IF(NSPTCO.NE.3 .AND. NSPTCO.NE.6) NSPTCO=NSPTCA
      WRITE(TAPOUT)
     1 NTAPA,NPG2A,ITRTA,NMPRM,NMBOD,JDGS0A,PRMA,GSCNA,LPRMA,LGSA,
     2 NUMDTA,NMDT1A,(JDDTA(I),I=1,NMDT1A),(DTA(I),I=1,NMDT1A),
     3 (LDTA(I),I=1,NMDT1A),NLABA,(LABA(I),I=1,NLABA),JDDT0A,LNKLVA,
     4 NSITCO,NSPTCO
     Z ,(IZERO,I=1,400)
      GOTO 990
C
C        TYPE 3
C
  300 IF(IO.GT.0.OR.TAPIN.LE.0) GOTO 305
      READ(TAPIN,ERR=910,END=920)
     1 NSEQA,NCODFA,NPLNTA,RITA,SERA,SITA,SPOTA,ERWGTA,ACCTMA,FDEVA,
     2 FREQA,ITIMA,NREWNA,NPG3A,PNAMA,NCENTA,
     3 JDPL0A,(PLCNA(I),I=1,NCNMXA),(LPLA(I),I=1,NCNMXA),
     4 JDSB0A,(SBCNA(I),I=1,NCNMXA),(LSBA(I),I=1,NCNMXA),
     5 ((LSCRDA(I,J),I=1,NSITCA),J=1,2),NRBSA,RBSA,LRBSA,
     6 NEQNA,EQNA,LEQNA,NCPHA,APHSA,LPHSA,NOBCNA,(OBSCNA(I),I=1,NOBCNA),
     7 ((SCORDA(I,J),I=1,NSITCA),J=1,2),KSITEA,(SPCDA(I),I=1,NSPTCA),
     8 (LSPCDA(I),I=1,NSPTCA),NCZNEA,N2A,(LCZHRA(I),I=1,N2A),
     9 NCTSSA,N3A,(LCCHRA(I),I=1,N3A),(LCSHRA(I),I=1,N3A),
     A NUMTRA,N4A,(NTRGA(I),(LTBODA(J,I),J=1,NCNMXA),NTZNEA(I),
     B (LTZHRA(J,I),J=1,4),NTTSSA(I),(LTCHRA(J,I),J=1,5),
     C (LTSHRA(J,I),J=1,5),I=1,N4A),N5A,N6A,(SCNAMA(J),
     D (LSCA(I,J),I=1,N6A),JDSC0A(J),(SCCNA(I,J),I=1,N6A),J=1,N5A),
     E NSZNEA,N7A,(LSZHRA(I),I=1,N7A),
     F NSTSSA,N8A,(LSCHRA(I),I=1,N8A),(LSSHRA(I),I=1,N8A),
     G NPLN2A,SPOT2A,(SPCD2A(I),I=1,NSPTCA),(LSPC2A(I),I=1,NSPTCA),
     H FREQ2A,LEMMNA,NEXTA,((EXNMSA(I,J),I=1,2),J=1,NEXTA),LNGDA,LNFORA,
     I CTLGA,NSKYCA,(LSKYA(I),I=1,NSKYCA),N9A,(LPSRCA(I),I=1,N9A),
     J NMPEXA,NAA,NBA,(NPLEXA(J),(LPEXA(I,J),I=1,NBA),J=1,NAA),T0SITA,
     K T0SPTA
      IF(NSITCA.LT.6) THEN
C FILL IN DEFAULT FOR SITE VELOCITY: T0=J2000, ZERO VELOCITY
         DO J=1,2
            T0SITA(J)=2451545D0
            DO I=NSITCA+1,6
               LSCRDA(I,J)=0
               SCORDA(I,J)=0._10
            END DO
         END DO
      ENDIF
      IF(NSPTCA.LT.6) THEN
C FILL IN DEFAULT FOR SP0T VELOCITY: T0=J2000, ZERO VELOCITY
         DO J=1,2
            T0SPTA(J)=2451545D0
            DO I=NSPTCA+1,6
               LSPCDX(I,J)=0
               SPCDX(I,J)=0._10
            END DO
         END DO
      ENDIF
      DO I=NCNMXA+1,NMBOD
         PLCNA(I)=0._10
         LPLA(I)=0
         SBCNA(I)=0._10
         LSBA(I)=0
         DO J=1,N4A
            LTBODA(I,J)=0
         END DO
         DO J=1,N5A
            SCCNA(I,J)=0._10
            LSCA(I,J)=0
         END DO
         DO J=1,NAA
            LPEXA(I,J)=0
         END DO
      END DO
      IF(NCODFA.EQ.0) IORTRN=0
      IF(NCODFA.GT.0) CALL FIXSHP(NSZNEA,NSTSSA,LSZHRA,LSCHRA,LSSHRA,
     1 LNFORA,LNGDA,24,N7A,N8A)
      IF(NLABA.LE.1) NEXTA=0
      IF(NSKYCA.LE.1) NSKYCA=0
C
  305 IF(IO.LT.0.OR.TAPOUT.LE.0) GOTO 990
      IF(NCODFA.EQ.0) THEN
         N2A=0
         N3A=0
         N4A=0
         N5A=0
         N6A=0
         N7A=0
         N8A=0
         N9A=0
         NMPEXA=0
         NOBCNA=0
         NEXTA=0
         NSKYCA=0
      ENDIF
      IF(N2A.LE.0) N2A=1
      IF(N3A.LE.0) N3A=1
      IF(N4A.LE.0) N4A=1
      IF(N5A.LE.0) N5A=1
      IF(N6A.LE.0) N6A=1
      IF(N7A.LE.0) N7A=1
      IF(N8A.LE.0) N8A=1
      IF(N9A.LE.0) N9A=1
      IF(NAA.LE.0 .OR. NBA.LE.1 .OR. NMPEXA.LE.0) THEN
         NAA=1
         NBA=1
         NPLEXA(1)=0
         LPEXA(1,1)=0
      ENDIF
      IF(N6A.LE.1) SCNAMA(1)='        '
      IF(NOBCNA.LE.0) THEN
         OBSCNA(1)=0._10
         NOBCNA=1
      ENDIF
      NEXT1=MAX0(NEXTA,1)
      NSKY1=NSKYCA
      IF(NSKYCA.LE.0) NSKY1=1
      WRITE(TAPOUT)
     1 NSEQA,NCODFA,NPLNTA,RITA,SERA,SITA,SPOTA,ERWGTA,ACCTMA,FDEVA,
     2 FREQA,ITIMA,NREWNA,NPG3A,PNAMA,NCENTA,JDPL0A,PLCNA,LPLA,JDSB0A,
     3 SBCNA,LSBA,((LSCRDA(I,J),I=1,NSITCO),J=1,2),NRBSA,RBSA,LRBSA,
     4 NEQNA,EQNA,LEQNA,NCPHA,APHSA,LPHSA,NOBCNA,(OBSCNA(I),I=1,NOBCNA),
     5 ((SCORDA(I,J),I=1,NSITCO),J=1,2),KSITEA,(SPCDA(I),I=1,NSPTCO),
     6 (LSPCDA(I),I=1,NSPTCO),NCZNEA,N2A,(LCZHRA(I),I=1,N2A),
     7 NCTSSA,N3A,(LCCHRA(I),I=1,N3A),(LCSHRA(I),I=1,N3A),
     8 NUMTRA,N4A,(NTRGA(I),(LTBODA(J,I),J=1,NMBOD),NTZNEA(I),
     9 (LTZHRA(J,I),J=1,4),NTTSSA(I),(LTCHRA(J,I),J=1,5),
     A (LTSHRA(J,I),J=1,5),I=1,N4A),N5A,N6A,(SCNAMA(J),
     B (LSCA(I,J),I=1,N6A),JDSC0A(J),(SCCNA(I,J),I=1,N6A),J=1,N5A),
     C NSZNEA,N7A,(LSZHRA(I),I=1,N7A),
     D NSTSSA,N8A,(LSCHRA(I),I=1,N8A),(LSSHRA(I),I=1,N8A),
     E NPLN2A,SPOT2A,(SPCD2A(I),I=1,NSPTCO),(LSPC2A(I),I=1,NSPTCO),
     F FREQ2A,LEMMNA,NEXT1,((EXNMSA(I,J),I=1,2),J=1,NEXT1),LNGDA,LNFORA,
     G CTLGA,NSKY1,(LSKYA(I),I=1,NSKY1),N9A,(LPSRCA(I),I=1,N9A),
     H NMPEXA,NAA,NBA,(NPLEXA(J),(LPEXA(I,J),I=1,NBA),J=1,NAA),T0SITA,
     Z T0SPTA,(IZERO,I=1,400)
      GOTO 990
C
C        TYPE 4
C
  400 IF(IO.GT.0.OR.TAPIN.LE.0) GOTO 405
      READ(TAPIN,ERR=910,END=920)
     1 NCODEA,IHRA,IMINA,SECA,(RESLTA(I),ERRORA(I),I=1,2),
     2 ATUTSA,UTUTSA,CLAMPA,LIMBA,OBSRVA,IMNTHA,IDAYA,IYEARA,JDSA,JDA,
     3 CTATA,CTRECA,NSAVA,(SAVA(I),I=1,NSAVA),NUM1A,NUMPRA,
     4 ((DERIVA(I,J),I=1,NUMPRA),J=1,NUM1A),
     5 ILDT1A,ILDT2A,ILDT3A,ILDT4A,ILDT5A,ILDT6A,
     6 NXPA,((DVXA(I,J),I=1,NXPA),J=1,NUM1A),
     7 NCALA,(CALA(I),SCALA(I),ICALA(I),I=1,NCALA),
     8 SUMCRA,LNSHBA,LXSHPA,(LSHOBA(I),I=1,LNSHBA)
      IF(NUMDTA.GT.0) CALL DTCKI(ILDT1A)
      IF(NCODEA.EQ.0) IORTRN=0
C
  405 IF(IO.LT.0.OR.TAPOUT.LE.0) GOTO 990
      IF(IO.GT.1) GOTO 930
      IF(NSAVA.LE.0) SAVA(1)=0._10
      IF(NSAVA.LE.0) NSAVA=1
      IF(NUM1A.LE.0) NUM1A=1
      IF(NUMPRA.LE.0) NUMPRA=1
      IF(NXPA.LE.0) NXPA=1
      IF(NCALA.LE.0) NCALA=1
      IF(LNSHBA.LE.0) LNSHBA=1
  408 WRITE(TAPOUT)
     1 NCODEA,IHRA,IMINA,SECA,(RESLTA(I),ERRORA(I),I=1,2),
     2 ATUTSA,UTUTSA,CLAMPA,LIMBA,OBSRVA,IMNTHA,IDAYA,IYEARA,JDSA,JDA,
     3 CTATA,CTRECA,NSAVA,(SAVA(I),I=1,NSAVA),NUM1A,NUMPRA,
     4 ((DERIVA(I,J),I=1,NUMPRA),J=1,NUM1A),
     5 ILDT1A,ILDT2A,ILDT3A,ILDT4A,ILDT5A,ILDT6A,
     6 NXPA,((DVXA(I,J),I=1,NXPA),J=1,NUM1A),
     7 NCALA,(CALA(I),SCALA(I),ICALA(I),I=1,NCALA),
     8 SUMCRA,LNSHBA,LXSHPA,(LSHOBA(I),I=1,LNSHBA)
     Z ,(IZERO,I=1,50)
      GOTO 990
C
C        READ ERROR, SET RETURN CODE
  910 IORTRN=-2
      GOTO 990
C
C        END OF FILE ENCOUNTERED, SET CODE
  920 IORTRN=-1
C
C        IF T4 RECORD AND ALSO WRITING, WRITE OUT RECORD WITH NCODE=0
      IF(TYPE.NE.4.OR.IO.LT.-1) GOTO 990
  930 NCODEA=0
      NSAVA=1
      NUM1A=1
      NUMPRA=1
      NXPA=1
      NCALA=1
      LNSHBA=1
      IF(TAPOUT.GT.0) GOTO 408
C
C
990   RETURN
      END
