      SUBROUTINE FIXSHP(NSZONE,NSTESS,LSZHAR,LSCHAR,LSSHAR,LNFOUR,
     1 LNGD,MAXZON,NZ1,NT1)
      IMPLICIT NONE
C           SUBR. FIXSHP - J.F.CHANDLER - 1980 OCT
C           CHANGE OLD-STYLE FOURIER SHAPE MODEL TO NEW ON OBSLIB.
C           TO BE CALLED FOR EACH TYPE 3 RECORD BEFORE COPYING OR
C           PROCESSING.
C
C     PARAMETERS
      INTEGER*2 NSZONE,NSTESS,LSZHAR(442),LSCHAR(209),LSSHAR(209),
     1 LNFOUR,LNGD,NZ1,NT1
      INTEGER*4 MAXZON
C
C NSZONE,NSTESS: LENGTHS OF ZONAL AND TESSERAL VECTORS
C LS.HAR: PARTIAL DERIVATIVE CONTROL VECTORS
C LNFOUR: =122 IF FOURIER SHAPE MODEL, =0 OTHERWISE
C LNGD: NUMBER OF GRID POINTS IF GRID SHAPE MODEL, 0 OTHERWISE
C MAXZON: ACTUAL SIZE OF ZONAL VECTOR
C NZ1,NT1: RETURNED AS MAX(1,NSZONE),MAX(1,NSTESS) FOR TYPE 3 RECORD
C
      INTEGER I,MXZ1
C
C           FIRST TEST IF ALREADY NEW STYLE
      IF(LNGD.GT.0.OR.LNFOUR.GT.0) GOTO 100
C           FIX ONLY FULL VECTORS
      IF(NSZONE.NE.19.OR.NSTESS.NE.54) GOTO 100
C           CHECK FOR REAL SPHERICAL HARMONIC INDICATORS
      DO 10 I=1,5
      IF(LSZHAR(I).NE.0.OR.LSZHAR(I+5).EQ.0) GOTO 100
   10 CONTINUE
C
C           MUST BE OLD-STYLE FOURIER MODEL, FIX IT
      LNFOUR=122
C           SHIFT VECTORS INTO PLACE
      DO 20 I=1,14
         LSZHAR(I)=LSZHAR(I+5)
   20 END DO

      DO 30 I=1,54
      LSZHAR(I+14)=LSCHAR(I)
         LSCHAR(I)=0
   30 END DO

      DO 40 I=1,54
         LSZHAR(I+68)=LSSHAR(I)
         LSSHAR(I)=0
   40 END DO
      GOTO 200
C
C           NOT OLD STYLE, BUT CHECK IF MAXZON IS BIGGER
  100 IF(LNFOUR.NE.122.OR.NSZONE.EQ.MAXZON) GOTO 999
C           YES, REPACK VECTORS
      MXZ1=NSZONE+1
      DO 110 I=MXZ1,LNFOUR
        LSZHAR(I)=LSCHAR(I-NSZONE)
        LSCHAR(I-NSZONE)=0
  110 END DO

C
C           RESET NSZONE, ETC.
  200 NSZONE=MAXZON
      NSTESS=LNFOUR-NSZONE
      IF(NSTESS.LT.0) NSTESS=0
      NZ1=NSZONE
      NT1=NSTESS
      IF(NT1.LT.1) NT1=1
C
  999 RETURN
      END
