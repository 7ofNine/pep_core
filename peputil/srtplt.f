      SUBROUTINE SRTPLT(PLMODE,DATA,N,K)

      IMPLICIT NONE

C          PLOT SORTED LOOKER DATA. IT INITIATES SORT
C     AND THEN PLOTS EACH POINT WITH THE APPROPRIATE SYMBOL.
C     BURST-MODE PLOTTING BREAKS ARE INSERTED AS NEEDED.
C
C        PARAMETERS
C             PLMODE - TYPE OF PLOTTING TO DO:
C                   0  IMMEDIATE LOOKER PLOT
C                   3  FINISH OFF ONEAXE PLOT
C             DATA   -  2-D ARRAY WITH (Y,X,SYMBOL,...) FOR EACH OF
C                       'N' POINTS
C             N   - NUMBER OF POINTS TOTAL
C             K   - REPEAT FACTOR OF ARRAY
C
      INTEGER*4 PLMODE,N,K
      REAL*4 DATA(9)
C
C        COMMON
      include 'burst.inc'
C           IBURST: SELECTOR FOR BURST OPTIONS
C                  0 - NON-BURST MODE
C                  1 - NOT USED
C                  2 - GAP MODE. BREAK WHENEVER A GAP IN DATA IS
C                      BIGGER THAN 'BGAP'
C                  3 - SPAN MODE. BREAK WHENEVER A POINT APPEARS
C                      MORE THAN 'BSPAN' FROM START OF THIS PLOT
      include 'graphs.inc'
      include 'misc.inc'
      include 'sortv.inc'
      include 'pltfrm.inc'
C
C        LOCAL
      REAL*4 RAT2,RATIO,SZALP,SZDLT,X,Y
      INTEGER*4 J,L
C
      MODPLT=0
C
      CALL FNAXE(PLMODE,N)
C
C        SCALE X-AXIS
C        NONZERO EXTRMX OVERRIDES SELF-SCALING FOR X DATA
      IF(IBURST.GT.1.AND.BURSTL.GT.0.) GOTO 15
      CALL SQALE(0,DATA(2),N,K,XL,EXTRMX,SMINX,1)
      GOTO 20
   15 CONTINUE
      SCONVX=BURSTS/BURSTL
   20 IF(IBURST.EQ.3) XLNOM=BURSTS/SCONVX
C
C        SCALE Y-AXIS
      CALL SQALE(0,DATA(1),N,K,YL,EXTRMY,SMINY,1)
C
C           ADJUST SCALE FOR ASPECT, IF NECESSARY
      IF(PLMODE.LT.0.OR.PLMODE.GT.1.OR..NOT.(TWOOBS.AND.ASPECT)) GOTO 25
      IF(NDV.NE.1.OR.NIV.NE.1.OR.(JTYPE.NE.2.AND.JTYPE.NE.8)) GOTO 25
      SZALP=(SMAXX-SMINX)*COS(0.01745329252*.5*(SMINY+SMAXY))
      IF(JTYPE.EQ.2) SZALP=SZALP*15.
      SZDLT=SMAXY-SMINY
      IF(SZALP.EQ.0..OR.SZDLT.EQ.0..OR.XL.EQ.0..OR.YL.EQ.0.) GOTO 25
      RATIO=SZDLT/SZALP
      RAT2=YL/XL
      IF(RATIO.LE.RAT2) GOTO 22
C           MUST SQUEEZE X-AXIS
      RATIO=RATIO/RAT2
      XL=XL/RATIO
      SCONVX=SCONVX*RATIO
      DATA(N*K+K+2)=SCONVX
      DIVX=DIVX*RATIO
      GOTO 25
C           MUST SQUEEZE Y-AXIS
   22 RATIO=RAT2/RATIO
      YL=YL/RATIO
      SCONVY=SCONVY*RATIO
      DATA(N*K+K+1)=SCONVY
      DIVY=DIVY*RATIO
   25 CONTINUE
C
      IF(PFIRST) CALL PLNITL
C
C           SORT DATA POINTS BY X
      CALL ABCSRT(DATA,N,K)
C
C           PLOT ALL POINTS
      L=N*K
      DO 80 J=1,L,K
      X1=DATA(J+1)
      IF(MODPLT.EQ.0) CALL PLAXIS(0)
      IF(IBURST.LT.2) GOTO 30
      IF(X1-X0.GT.BURSTS) CALL PLAXIS(2)
      IF(IBURST.EQ.2) X0=X1
   30 CONTINUE
      X=(X1-SMINX)/SCONVX
      Y=(DATA(J)-SMINY)/SCONVY
C        PLOT ONE POINT
      IF(MODPLT.EQ.2.AND..NOT.SYMBEL) GOTO 40
      CALL SYMBOL(X,Y,0.07,DATA(J+2),0.0,-1)
      IF(SYMBEL) GOTO 50
   40 CALL PLOT(X,Y,4-MODPLT)
      MODPLT=2
   50 X2=X1
   80 CONTINUE
C
C        PLOT THE LAST X-AXIS
  100 CALL PLAXIS(1)
      RETURN
      END
