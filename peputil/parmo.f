      SUBROUTINE PARMO(MAXPT,DATA)
C         ADJUST AND CHECK INPUT PARAMETERS.
C         COMPUTES SPAN PARAMETERS IN UNITS OF DAYS IF GIVEN IN PARTS
C         OF A DAY.  DECIDES WHETHER TO USE FOUR1 FROM VALUE OF NPOINT.
C         SOLVES FOR MISSING SPAN PARAMETERS.  CHECKS ARRAY DIMENSIONS
C         AGAINST SIZE OF ANALYSIS DESIRED.
C           ZEROES OUT NEEDED PART OF DATA ARRAYS
C
C        PARAMETERS
      INTEGER*4 MAXPT
      REAL*4 DATA(9)
C           MAXPT  - NUMBER OF FULLWORDS IN WORK ARRAY
C           DATA   - WORK ARRAY
C
C         COMMON
      include 'inodtabc.inc'
      include 'hrmnsc.inc'
      include 'misc.inc'
      include 'span.inc'
C
C         LOCAL VARIABLES
      REAL*8 SIZRUN
      INTEGER*4  N
      LOGICAL*4 ONEZ
C
C         MAKE SURE AVGSPN APPLIES TO MORE THAN ONE SPAN
      IF(AVGSPN .AND. NSPAN.EQ.1) AVGSPN = .FALSE.
C
C         GET USEF1
      IF(NPOINT.LT.0) THEN
         LOG2N = -NPOINT
         NPOINT = 2**LOG2N
         USEF1 = .TRUE.
      ENDIF
C
C         COMPLETE TIME PARAMETERS
      SIZSPN = SIZSPN + SZHR/24.0 + SZMIN/1440.0 + SZSEC/86400.0
      DELTA = DELTA + DEHR/24.0 + DEMIN/1440.0 + DESEC/86400.0
C
C         GET MISSING SPAN PARAMETER
      SIZRUN = STOP - START
      IF(SIZSPN.GT.SIZRUN) SIZSPN = SIZRUN
      ONEZ = .FALSE.
      IF(NPOINT.EQ.0) THEN
         NPOINT = SIZSPN/DELTA
         ONEZ = .TRUE.
      ENDIF
      IF(DELTA.EQ.0) THEN
         IF(ONEZ) GOTO 50
         DELTA = SIZSPN/NPOINT
         ONEZ = .TRUE.
      ENDIF
      IF(SIZSPN.EQ.0) THEN
         IF(ONEZ) GOTO 50
         SIZSPN = NPOINT*DELTA
         ONEZ = .TRUE.
      ENDIF
      IF(ONEZ) GOTO 60
   50 WRITE(PRINT,1000)
 1000 FORMAT ('1ERROR IN SPAN PARAMETERS.'/ ' EITHER TWO OR MORE ',
     1 'OF (NPOINT, DELTA, SIZSPN) ARE ZERO OR NONE ARE ZERO.')
      STOP
   60 CONTINUE
C
C         WRITE SPAN PARAMETERS
      WRITE(PRINT,1002) START, STOP, NPOINT, DELTA, SIZSPN
 1002 FORMAT ('1*** SPAN PARAMETERS ***'//
     1 ' START  =', 1PD24.15/
     2 ' STOP   =', 1PD24.15//
     3 ' NPOINT =', I24/
     4 ' DELTA  =', 1PD24.15/
     5 ' SIZSPN =', 1PD24.15)
C
C         IF PADDING ZEROES, DOUBLE NPOINT NOW
      IF(PADZ) THEN
         NPOINT = 2*NPOINT
         IF(USEF1) LOG2N=LOG2N+1
      ENDIF
C
C         CHECK ARRAY DIMENSIONS
      M=7*NSPAN*(NPOINT+2)
      IF(M.GT.MAXPT) THEN
         WRITE(PRINT,1001) MAXPT
 1001    FORMAT ('1ARRAY SIZE OF ',I6,' WORDS EXCEEDED.')
         STOP
      ENDIF
C           CLEAR DATA ARRAY INITIALLY
      DO 80 I=1,M
   80 DATA(I)=0.
      RETURN
      END
