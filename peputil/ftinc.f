      SUBROUTINE FTINC(DATA,MAT,N,DT,DEPVAR,SIG)
C FTINC - J.F.CHANDLER - 2005 SEP 20
      IMPLICIT NONE
C PERFORM ONE STEP OF ACCUMULATING FOURIER TRANSFORM
C
C PARAMETERS
      INTEGER N
      REAL DATA(2,N),MAT(4,N)
      REAL DT,DEPVAR,SIG
C DATA  - TRANSFORM OUTPUT ARRAY
C N     - SIZE OF DATA ARRAY
C DT    - NORMALIZED INDEPENDENT VARIABLE, RANGE=(0,1) OR (0,-1) IF BACK
C DEPVAR- DEPENDENT VARIABLE TO BE TRANSFORMED
C SIG   - UNCERTAINTY OF DEPVAR

C LOCAL VARIABLES
      REAL TWOPI/6.283185/,ANG,CX,SX,WT
      INTEGER I1

      IF(SIG.LE.0.) THEN
         WT=0.
      ELSE
         WT=1./SIG**2
      ENDIF
      DO I1=1,N
         ANG=TWOPI*DT*(I1-1)
         CX=COS(ANG)
         SX=SIN(ANG)
         DATA(1,I1)=DATA(1,I1)+DEPVAR*CX*WT
         DATA(2,I1)=DATA(2,I1)+DEPVAR*SX*WT
         MAT(1,I1)=MAT(1,I1)+CX*CX*WT
         MAT(2,I1)=MAT(2,I1)+CX*SX*WT
         MAT(3,I1)=MAT(3,I1)+CX*WT
         MAT(4,I1)=MAT(4,I1)+SX*WT
      END DO
      DATA(2,1)=DATA(2,1)+DEPVAR**2*WT
      IF(WT.GT.0) MAT(2,1)=MAT(2,1)+1.
      RETURN
      END
