      SUBROUTINE QSORT(A,N,SIZE,COMP)
      IMPLICIT NONE
C  SORT ARRAY A, OF LENGTH N, INTO ASCENDING ORDER.  RUNNING TIME IS
C  PROPORTIONAL TO N*LOG(N), RATHER THAN THE NAIVE N**2.  ADAPTED
C  FROM SORT, BY RICHARD SINGLETON, IN COMM. OF ACM, MARCH, 1969.
C        LOGIC CLEANED UP 1980 MAY - JFC
C  EACH ITEM IS 'SIZE' BYTES LONG
C  INTEGER FUNCTION 'COMP' RETURNS A VALUE -/0/+ ACCORDING TO WHETHER
C  THE FIRST COMPARAND IS LESS THAN, EQUAL TO, OR GREATER THAN THE SECOND
      INTEGER*4 N,SIZE
      INTEGER*2 COMP
      EXTERNAL COMP
      CHARACTER*(SIZE) A(N)

      CHARACTER*100 TEMP
      INTEGER*4 NMID/999/,MIDDL(999),FRST,LAST,I1,I2,ISW,ISZ,IMID,
     . MED,MED2,CENT,INVRT

      IF(N.LE.1) RETURN
      FRST=1
      LAST=N
      IMID=0
   10 DO WHILE(LAST-FRST.GE.10)
C ESTIMATE THE MEDIAN ELEMENT OF A.
C AMED=MAX(MIN(A(FRST),A(LAST)),MIN(A(CENT),MAX(A(FRST),A(LAST))))
         CENT=(FRST+LAST)/2
         MED=CENT
         IF(COMP(A(MED),A(FRST)).LT.0) MED=FRST
         IF(COMP(A(MED),A(LAST)).LE.0) GOTO 140
         MED2=FRST+CENT-MED
         MED=LAST
         IF(COMP(A(MED),A(MED2)).LT.0) MED=MED2
C ROUGH SORT A INTO TWO STRINGS -- THOSE LESS THAN AMED AND THOSE GREATER.
  140    I1=FRST-1
         I2=LAST+1
  150    I1=I1+1
         IF(COMP(A(I1),A(MED)).LT.0) GOTO 150
         IF(I1.GE.I2) GOTO 200
  170    I2=I2-1
         IF(I1.GE.I2) GOTO 200
         IF(COMP(A(MED),A(I2)).LE.0) GOTO 170
         DO ISW=1,SIZE,100
            ISZ=MIN(SIZE,ISW+99)
            TEMP=A(I1)(ISW:ISZ)
            A(I1)(ISW:ISZ)=A(I2)(ISW:ISZ)
            A(I2)(ISW:ISZ)=TEMP
         END DO
         IF(I1.EQ.MED) THEN
            MED=I2
         ELSE IF(I2.EQ.MED) THEN
            MED=I1
         ENDIF
         GOTO 150
C SAVE THE END OF THE SECOND STRING AND SORT THE FIRST ONE
  200    IMID=IMID+1
         IF(IMID.GT.NMID) GOTO 260
         MIDDL(IMID)=LAST
         IF(I2.GT.FRST) THEN
            I2=I2-1
         ELSE
C THE MEDIAN IS ACTUALLY THE MINIMUM
            IF(MED.NE.FRST) THEN
               DO ISW=1,SIZE,100
                  ISZ=MIN(SIZE,ISW+99)
                  TEMP=A(MED)(ISW:ISZ)
                  A(MED)(ISW:ISZ)=A(FRST)(ISW:ISZ)
                  A(FRST)(ISW:ISZ)=TEMP
               END DO
               MED=FRST
            ENDIF
         ENDIF
         LAST=I2
      END DO
C SHUTTLE SORT SHORT STRINGS
      I2=LAST-1
      INVRT=1
      DO WHILE(INVRT.GT.0 .AND. FRST.LE.I2)
         INVRT=0
         DO I1=FRST,I2
            IF(COMP(A(I1+1),A(I1)).LT.0) THEN
               INVRT=1
               DO ISW=1,SIZE,100
                  ISZ=MIN(SIZE,ISW+99)
                  TEMP=A(I1)(ISW:ISZ)
                  A(I1)(ISW:ISZ)=A(I1+1)(ISW:ISZ)
                  A(I1+1)(ISW:ISZ)=TEMP
               END DO
            ENDIF
         END DO
         I2=I2-1
      END DO
C THE FIRST STRING HAS BEEN COMPLETELY SORTED.  SORT THE SECOND ONE.
      IF(IMID.LE.0) RETURN
      FRST=LAST+1
      LAST=MIDDL(IMID)
      IMID=IMID-1
      GOTO 10
  260 WRITE(6,270) NMID
  270 FORMAT('0ERROR IN SORT--MORE THAN ',I3,' SUBSTRINGS.')
      STOP 10
      END
