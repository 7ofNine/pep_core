      SUBROUTINE PLNEXT(XLF,YLFI,IRUI)
C           RESET ORIGIN FOR NEXT PLOT OR TITLE
C
C        PARAMETERS
C           XLF - X SIZE OF NEXT BLOCK IN INCHES (MAY BE APPROX.)
C           YLF - Y SIZE OF NEXT BLOCK IN INCHES (EXACT)
C           IRU - IF ZERO, NEXT REQUEST CAN BE STACKED UP
C                 IF POSITIVE, NEXT REQUEST MUST BE TO THE RIGHT
C
      REAL*4 XLF,YLF,YLFI
      INTEGER*4 IRU,IRUI
C
C        COMMON
      include 'graphs.inc'
      include 'misc.inc'
      include 'pltfrm.inc'
      include 'pltlab.inc'
      include 'plttyp.inc'
C
C        LOCAL
      REAL*4 XZ,YZ,XNEW,YNEW
C
      IRU=IRUI
      YLF=YLFI
C           COLLECT GREATEST EXCURSIONS
      IF(XEND.GT.XEMAX) XEMAX=XEND
      IF(YEND.GT.YEMAX) YEMAX=YEND
C
C           SEE IF PLOT WILL FIT IN THIS FRAME
      IF(XOMIN+XLF.GT.XFRAME) GOTO 300
      IF(ISW.GT.0) GOTO 170
C           MAY STACK PLOTS, CHECK HEAD ROOM
  150 IF(YEMAX+YLF.GT.YFRAME) GOTO 160
      XNEW=XOMIN
      YNEW=YEMAX
      GOTO 370
C           NO ROOM TO STACK, START NEW COLUMN OR FRAME
  160 IF(ISW.GT.0) GOTO 300
C           NEW COLUMN, CHECK ELBOW ROOM
      IF(XEMAX+XLF.GT.XFRAME) GOTO 300
      GOTO 360
C           MUST MOVE RIGHT
  170 IF(XEND+XLF.GT.XFRAME) GOTO 150
      XNEW=XEND
      YNEW=YORG
      GOTO 370
C           MUST START NEW FRAME
  300 GOTO (310,310,330,340,330,342,360,330,330), LOCALE
C           DRAPER CALCOMP OR PRINTER
  310 CALL PLOTS(0,0,88)
      GOTO 355
C           DRAPER VERSATEC OR HP TERMINAL OR APPLE LW OR MS-KERMIT
  330 CALL PLOT(0.,0.,-999)
      GOTO 355
C           IPC CALCOMP
  340 CONTINUE
C           OIT CALCOMP
  342 CONTINUE
      GOTO 360
C
C           STARTING NEW FRAME/PLOT - INITIALIZE PEN POSITION
  355 XEMAX=XMRG
      XORG=0.0
      YORG=0.0
C           STARTING NEW COLUMN, UPDATE MARGINS
  360 XOMIN=XEMAX
      YEMAX=YMRG
      XNEW=XOMIN
      YNEW=YMRG
C           RESET PLOT ORIGIN TO (XNEW,YNEW)
  370 CALL PLOT(XNEW-XORG,YNEW-YORG,-3)
      XORG=XNEW
      YORG=YNEW
      XEND=XORG
      YEND=YORG
C           RESET PLOTTING UPPER LIMITS
      XZ=XORG+0.1
      YZ=YORG+YLF
      IF(XZ.GT.XEND) XEND=XZ
      IF(YZ.GT.YEND) YEND=YZ
      ISW=IRU
      RETURN
C
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
      ENTRY PLNITL
C           INITIALIZE PLOTTING (ONCE PER JOB)
C
      XZ=0.0
      YZ=0.0
      YLK=0.0
      IRU=0
      PFIRST=.FALSE.
      GOTO (310,310,310,440,310,310,470,310,310), LOCALE
C
  440 CALL NEWPLT(PROBNO,PROGNO)
      GOTO 310
  470 CALL PLOTS(-3,-500,88)
      GOTO 355
C
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
      ENTRY PLFINL
C
C           CLOSE PLOT OUTPUT
C
      IF(XEND.GT.XEMAX) XEMAX=XEND
      IF(LOCALE.EQ.7) CALL PLOT(XEMAX-XORG,-YORG,3)
      CALL PLOT(0.,0.,999)
      IF(LOCALE.EQ.4) CALL ENDPLT
      RETURN
C
      END
