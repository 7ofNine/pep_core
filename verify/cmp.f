      LOGICAL FUNCTION CMP(BUFA,BUFB)
      IMPLICIT NONE
C LOGICAL FUNCTION CMP(BUFA,BUFB) - J.F.CHANDLER - 1982 MARCH
C RETURNS THE VALUE 'TRUE' IF THE TWO LINES MATCH
 
      CHARACTER*256 BUFA,BUFB
 
      include 'lcmp.inc'
      include 'ptrs.inc'
C
      INTEGER*4 JB, JPTX, IPTA, IPTB
      LOGICAL*4 SCMP
      CHARACTER*205 BUFTA,BUFTB
      INTEGER*4 I, J, LIM, LIM1, LIM5, IP5, IP1, IM2
      CHARACTER*9 PEPSTR(8)/'TASK TIME', 'REAL TIME', 'RROR ANAL',
     . 'PEP VERSI','TOTAL EXE', 'TERATION=', ' OF YEAR=', 'CK TIME ='/
      CHARACTER*1 IGNCHR(5)/' ','+','D','E','Q'/
C
      CMP=.FALSE.
      DO 10 JB=1,ISF
      JPTX=ISTRT(JB)
      IF(BUFA(JPTX:IFIN(JB)).NE.BUFB(JPTX:IFIN(JB))) GOTO 100
   10 CONTINUE
C THEY MATCH
   20 CMP=.TRUE.
      RETURN
 
C          NOT IDENTICAL, CHECK FOR SPECIAL CONSIDERATIONS
  100 IF(LCMP.LT.0) THEN
 
C IDENTITY REQUIRED, THAT'S ALL
         RETURN
 
      ELSE IF(LCMP.EQ.0) THEN
 
C COMPARE ONLY NON-BLANKS
         GOTO 200
C
      ELSE IF(LCMP.EQ.1) THEN
 
C IGNORE FORTRAN LABELS
 
C NO MATCH IF COMPARISON WAS OK UNTIL AFTER COLUMN 6
C NO MATCH IF EITHER CARD IS A COMMENT
         IF(JPTX.GE.7 .OR. BUFA(1:1).EQ.'C' .OR. BUFB(1:1).EQ.'C')
     .     RETURN
         CMP=(BUFA(7:72).EQ.BUFB(7:72))
         RETURN
C
      ELSE IF(LCMP.EQ.2) THEN
 
C ACCEPT FORTRAN LABELS LEFT-JUSTIFIED
         IF(BUFA(1:1).EQ.'C' .OR. BUFB(1:1).EQ.'C') RETURN
C NOT COMMENT, JUST COMPARE NON-BLANKS
         GOTO 200
C
      ELSE IF(LCMP.EQ.3) THEN
 
C HANDLE PEP-ISMS
         LIM=MIN0(JSIZ-8,40)
         DO 110 I=1,LIM
         DO 110 J=1,8
         IF(BUFA(I:I+8).EQ.PEPSTR(J) .AND. BUFB(I:I+8).EQ.PEPSTR(J))
     .      GOTO 20
  110    CONTINUE
         LIM=JSIZ-10
         DO 120 I=1,LIM
         IF(BUFA(I:I+5).EQ.'LEVEL=' .AND. BUFB(I:I+5).EQ.'LEVEL=')
     .      GOTO 20
  120    CONTINUE
C
         LIM=MIN0(205,JSIZ)
         LIM1=LIM-1
         LIM5=LIM-5
         BUFTA(1:LIM)=BUFA(1:LIM)
         BUFTB(1:LIM)=BUFB(1:LIM)
         DO 140 I=1,LIM1
         IF(BUFTA(I:I).NE.'-' .OR. BUFTB(I:I).NE.' ') GOTO 140
         IP1=I+1
         DO 130 J=IP1,LIM
         IF(BUFTA(J:J).EQ.'0' .OR. BUFTA(J:J).EQ.'.') GOTO 130
         IF(BUFTA(J:J).LE.'0' .OR. BUFTA(J:J).GT.'9') BUFTA(I:I)=' '
         GOTO 140
  130    CONTINUE
         BUFTA(I:I)= ' '
  140    CONTINUE
         DO 150 I=1,LIM
         DO 150 J=1,5
         IF(BUFTA(I:I).EQ.IGNCHR(J)) BUFTA(I:I)='0'
         IF(BUFTB(I:I).EQ.IGNCHR(J)) BUFTB(I:I)='0'
  150    CONTINUE
         DO 160 I=3,LIM5
         IF(BUFTA(I:I).NE.'/' .OR. BUFTB(I:I).NE.'/') GOTO 160
         IF(BUFTA(I+3:I+3).NE.'/' .OR. BUFTB(I+3:I+3).NE.'/') GOTO 160
         IM2=I-2
         IP5=I+5
         DO 155 J=IM2,IP5
         IF(BUFTA(J:J).EQ.' ' .OR.
     .    (BUFTA(J:J).GE.'0'.AND.BUFTA(J:J).LT.'9')) BUFTA(J:J)='9'
         IF(BUFTB(J:J).EQ.' ' .OR.
     .    (BUFTB(J:J).GE.'0'.AND.BUFTB(J:J).LT.'9')) BUFTB(J:J)='9'
  155    CONTINUE
  160    CONTINUE
 
         IF(BUFTA(1:LIM).EQ.BUFTB(1:LIM)) GOTO 20
         GOTO 200
C
      ELSE
C OTHER ...
         CMP=SCMP(BUFA,BUFB,JPTX,JSIZ)
         RETURN
      ENDIF
 
C          COMPARE FROM CURRENT POINTERS TO END OF LAST FIELD
C          IGNORE BLANKS IN THE COMPARISON, EXIT IF 'EQUAL'
C          RETURN IF NOT
  200 IPTA=JPTX
      IPTB=JPTX
  210 IF(IPTA.GT.IFIN(ISF) .OR. BUFA(IPTA:IPTA).NE.' ') GOTO 220
      IPTA=IPTA+1
      GOTO 210
  220 IF(IPTB.GT.IFIN(ISF) .OR. BUFB(IPTB:IPTB).NE.' ') GOTO 230
      IPTB=IPTB+1
      GOTO 220
  230 IF(IPTA.GT.IFIN(ISF) .AND. IPTB.GT.IFIN(ISF)) GOTO 20
      IF(IPTA.GT.IFIN(ISF) .OR. IPTB.GT.IFIN(ISF)) RETURN
      IF(BUFA(IPTA:IPTA).NE.BUFB(IPTB:IPTB)) RETURN
      IPTA=IPTA+1
      IPTB=IPTB+1
      GOTO 210
C
      END
